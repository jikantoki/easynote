;出力する時の名前の設定
#packopt name "notepad"
;	モジュールをインクルード
#include "mod_menu_new.as"
#include "user32.as"

;	下位ワード取り出しマクロ
#define ctype LOWORD(%1) (%1 & $FFFF)

;その他定数
#define MEMOTITLE "メ↑モ↓"
#define SCREENX 800
#define SCREENY 600
#define TEXTLONG 320000

#define KEYCTRL 17
#define KEYS 83
#define KEYO 79

;	項目ID用マクロ
#enum ID_QUIT = 0
#enum ID_OPEN = 1
#enum ID_SAVE = 2
#enum ID_ABOUT= 3
#enum ID_HOYON= 4
#enum ID_RIGHT= 5
#enum ID_SAVED= 6
#enum ID_NEW  = 7

;スタート地点
*start

;メモ内容の保存
sdim buf,TEXTLONG
notesel buf
;ファイルパスの保存
sdim filepath,512

;一時的な設定の保存
rightbar=0
saveas=0
newfile=1

;画面の準備
title MEMOTITLE
screen 0, ginfo(20),ginfo(21),4
width SCREENX,SCREENY
onexit goto *exit

;	サイズ可変ウィンドウ化
	GetWindowLong hwnd, -16
	SetWindowLong hwnd, -16, stat | $10000 | $40000

;	画面再壁画のサブルーチン
	oncmd gosub *wordinput, 5

;ダイアログ用の変数
	infobox="メモ帳\nT3A作成"

;	メッセージ割り込み指定（後で説明）
	oncmd gosub *command, WM_COMMAND

;	[ファイル]-[サブ]の２階層目メニュー
	newmenu hSubmenu.0, 1
		addmenu hSubmenu.0, "ときえのきって誰だよその女！(&T)", ID_HOYON

;	[ファイル]の１階層目メニュー
	newmenu hSubmenu.1, 1
		addmenu hSubmenu.1, "新規(&N)", ID_NEW
		addmenu hSubmenu.1, "開く(&O)", ID_OPEN
		addmenu hSubmenu.1, "名前を付けて保存", ID_SAVE
		addmenu hSubmenu.1, "保存(&S)",ID_SAVED
		addmenu hSubmenu.1, "なんとなくサブメニュー(&F)", hSubmenu.0, $10
		addmenu hSubmenu.1, "", 0, $800
		addmenu hSubmenu.1, "終了(&X)", ID_QUIT

;	[ヘルプ]の１階層目メニュー
	newmenu hSubmenu.2, 1
		addmenu hSubmenu.2, "バージョン情報(&A)", ID_ABOUT
		
;	[設定]の１階層目メニュー
	newmenu hSubmenu.3, 1
		addmenu hSubmenu.3, "右スクロールの表示・非表示(&R)", ID_RIGHT

;	０階層目のバー
	newmenu hMenu, 0
		addmenu hMenu, "ファイル(&F)", hSubmenu.1, $10
		addmenu hMenu, "設定(&T)", hSubmenu.3, $10
		addmenu hMenu, "ヘルプ(&H)", hSubmenu.2, $10

;	メニューバーをウィンドウに反映
	applymenu hMenu

	oncmd gosub *command, WM_COMMAND

;文字入力ルーチンに飛ばす
	wait 2
	gosub *wordinput

	onkey gosub *inkey


	stop
	

;	WM_COMMANDメッセージ用

*command
	if lparam = 0 {
;		wparamの下位ワードの数値を取り出す
		wID = LOWORD(wparam)

		switch wID
		case 0;終了
			goto *exit
			swbreak
		case 1;ファイルオープン
			gosub *fileopen
			gosub *wordinput
			swbreak
		case 2;ファイルセーブ
			saveas=0
			gosub *filesave
			gosub *wordinput
			swbreak
		case 3;アプリ情報
			dialog infobox
			swbreak
		case 4;誰よその女
			dialog "児島だよ！"
			swbreak
		case 5;サイドバー設定
			if rightbar=0 : rightbar=1 : else : rightbar=0
			gosub *wordinput
			swbreak
		case 6;名前を付けて保存
			saveas=1
			gosub *filesave
			gosub *wordinput
			swbreak
		case 7;新規ファイル
			newfile=1
			buf=""
			gosub *wordinput
			title MEMOTITLE
			swbreak
		default
			swbreak
		swend

		return
	}

	return

*exit
	dialog "本当に終了するんか？",3
	if stat = 6{
		end
	}
	stop


*wordinput
	
;ここから文字入力部
	clrobj 0
	pos 0,0
	if rightbar=0 {
		mesbox buf,ginfo_sizex-14,ginfo_sizey-57,5
	}else{
		mesbox buf,ginfo_sizex-14,ginfo_sizey,
	}
	objsel 0
	return

*fileopen
	dialog "txt",16,"テキストファイル"
	if stat=1 {
		newfile=0
		filepath=refstr
		notesel buf
		noteload filepath
		title MEMOTITLE + "    " + filepath
	}
	return

*filesave
	stata=0
	if saveas=1 or newfile=1 {
		dialog "txt",17,"テキストファイル"
		if stat=1 {
			stata=stat
			filepath=refstr + ".txt"
		}
	}
	if saveas=0 or stata=1 or newfile=1 {
		newfile=0
		notesel buf
		notesave filepath
		title MEMOTITLE + "    " + filepath
	}
	return

*inkey
	getkey whichkey1,KEYCTRL
	getkey whichkey2,KEYS
	getkey whichkey3,KEYO
	if whichkey1=1 & whichkey2=1 : gosub *filesave
	if whichkey1=1 & whichkey3=1 : gosub *fileopen

	return


